buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath files(mkdir('build/classes/java/main'))
        classpath 'org.robotframework:swinglibrary:1.9.8'
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'org.roboscratch.robot' version '0.1.2'
}
 
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

project.ext {
    javaVersion = '1.8'
    junitVersion = '4.12'
}

compileJava {
    targetCompatibility = javaVersion
    sourceCompatibility = javaVersion
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.18'
    implementation 'org.glassfish:javax.json:1.1.2'
    testImplementation 'junit:junit:' + junitVersion
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-proc:none', '-Xlint:unchecked', '-Xlint:deprecation']
}

version = '1.0'
mainClassName = 'edu.wofford.Main'

jar {
    archiveBaseName = 'HelloWorld'
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        html {
            enabled true
        }
    }
 
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['edu/wofford/wocoin/Utilities**',
                                        'edu/wofford/wocoin/main/**'])
        }))
    }
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }
    exclude 'edu/wofford/Main*'
    exclude 'edu/wofford/ConsoleMain*'
    exclude 'edu/wofford/GuiMain*'
    exclude 'keywords/**'
}

runrobot.data_sources = 'src/acceptance'
runrobot.outputdir = 'build/reports/robot'
runrobot.debugfile = 'debug.log'
runrobot.dependsOn build
